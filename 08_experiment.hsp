// INFO ************************************************
// FileName : 08_experiment.hsp
// Date   : 2022/11/02
// Author : YUZURANIUM（ゆずらにうむ）
// URL    : https://twitter.com/YUZRANIUM
//******************************************************


#if 0
#packopt name "08_ver030"
#packopt type 0
#packopt version "text/08version.txt"
/*##############################################################################
							画面切り替え機能実験
###############################################################################*/
	#include "user32.as"
	#include "gdi32.as"
	#include "hsedsdk.as"
	#include "sqlele.hsp"
	#include "00_module.hsp"
	#include "00_mylv.hsp"
	/***ウィンドウID***/
	#enum MASTER_ex = 0
	#enum MAIN_ex
	#enum SUB1_ex
	#enum SUB2_ex

	#enum tabid1
	#enum tabid2
	#enum tabid3
	#enum tabid4
	#enum tabid5
	/*** 各種変数定義 ***/

	//HSPのバージョン
	hspv = "" : hsed_cnvverstr hspver : hspv += refstr
	if instr(hspv, 0, "b") != -1 : strrep hspv, "b", "beta"
	title " 08_ver0.3.0experiment  for  HSP " + hspv
	sdim logfile_ex, 2048
	notesel logfile_ex
	noteload "text\\08_ver000ex.txt" : noteunsel
	link = "https://github.com/YUZRANIUM/PC_CareingDiary"
	oncmd 0

/*******************************************************************************
								MASTER_ex
********************************************************************************/

	screen MASTER_ex, 1100, 650, 2 : font "メイリオ", 16
	hwindow = hwnd
	mref BMSCRn, 67 : hFontn = BMSCRn.38

	sdim tabname, 64 : sdim mousmoni, 64
	tabname = "Tab1", "Tab2", "Tab3", "Tab4", "Tab5"
	sdim cpu, 2048 : sdim data_cpu, 1024 : sdim cpu_culm, 64 : dim c_culm_num
	sdim gpu, 2048 : sdim data_gpu, 1024 : sdim gpu_culm, 64 : dim g_culm_num
	sdim rom, 2048 : sdim data_rom, 1024 : sdim rom_culm, 64 : dim r_culm_num
	cpu_culm = "" : gpu_culm = "" : rom_culm = "" : all_clum = ""

	swi = 1
	c_clum_w = 50, 55, 75, 80, 80, 80
	g_clum_w = 50, 55, 75, 80, 80, 80
	r_clum_w = 50, 55, 75, 80, 80, 80
	dbfiles = "SQL\\Syouhin.db"

	sql_open dbfiles
	sql_q "BEGIN;"

	sql_q "SELECT * FROM MyCPU;"
	//レコードの数
	cpu_cunt = stat
	//カラムの数
	c_culm_num = length(tmparr)
	//カラムの一覧リスト
	cpu_culm = sql_collist() : split cpu_culm, ",", cpu_culm
	//DBのレコードを文字列型1次元配列変数に格納する
	myindata cpu_cunt, c_culm_num, cpu_culm, cpu

	sql_q "SELECT * FROM MyGPU;"
	gpu_cunt = stat
	gpu_culm = sql_collist() : split gpu_culm, ",", gpu_culm
	g_culm_num = length(tmparr)
	myindata gpu_cunt, g_culm_num, gpu_culm, gpu

	sql_q "SELECT * FROM MyROM;"
	rom_cunt = stat
	rom_culm = sql_collist() : split rom_culm, ",", rom_culm
	r_culm_num = length(tmparr)
	myindata rom_cunt, r_culm_num, rom_culm, rom

	sql_q "COMMIT;"
	sql_close

	/***MASTERここまで***/


/*******************************************************************************
								MAIN_ex
********************************************************************************/

	bgscr MAIN_ex, 1100, 625, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hwindow

	font "メイリオ", 14 : objmode 2, 0

	pos 10, 0 : mes dbfiles : x2 = ginfo(14)
	objsize 150, 25 : pos x2 + 20, 0

	button gosub "Your Get List Items", *getitem
	id_butget = stat  :  hButget = objinfo_hwnd(id_butget)

	pos x2 + 300, 0
	button gosub "SUB1_exへ", *onsub1
	id_gowin = stat  :  hButnew = objinfo_hwnd(id_gowin)

	pos x2 + 300 + 180, 0
	button gosub "SUB2_exへ", *onsub2

	pos 20, 30
	/***リストビューの設置***/
	mylv 500, 280, id_cpumoni, hLVcsouko
	incolm hLVcsouko, cpu_culm, c_culm_num, c_clum_w
	insqlitem hLVcsouko, cpu, cpu_cunt, c_culm_num

	pos 550, 30
	/***リストビューの設置***/
	mylv 500, 280, id_gpumoni, hLVgsouko
	incolm hLVgsouko, gpu_culm, g_culm_num, g_clum_w
	insqlitem hLVgsouko, gpu, gpu_cunt, g_culm_num

	pos 20, 320
	/***リストビューの設置***/
	mylv 500, 280, id_rommoni, hLVrsouko
	incolm hLVrsouko, rom_culm, r_culm_num, r_clum_w
	insqlitem hLVrsouko, rom, rom_cunt, r_culm_num

	oncmd gosub *listnotify, WM_NOTIFY

	/******フォントの反映******/
	sendmsg hLVcsouko, $30, , hFontn
	sendmsg hLVgsouko, $30, , hFontn
	sendmsg hLVrsouko, $30, , hFontn
	/***SUB1_exここまで***/


/*******************************************************************************
								SUB1_ex
********************************************************************************/

	bgscr SUB1_ex, 1100, 625, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hwindow

	font "メイリオ", 16 : objmode 2, 0

	pos 20, 0
	mes "bgscr命令による専用画面遷移動作実験"
	x1 = ginfo(14)
	mes "こちらはSUB1_exです。"

	objsize 100, 25

	pos x1 + 30, 0
	button gosub "MAIN_exへ", *onmain

	pos x1 + 30 + 160, 0
	button gosub "SUB2_exへ", *onsub2

	pos 875, 50
	mesbox mousmoni, 200, 30, 0
	id_mousmoni = stat

	pos 10, 50
	font msgothic, 15
	mesbox logfile_ex, 845, 500, 0

	pos 10, 555
	mylmb "GitHub", 150, 25, hlink1, SS_CENTER | WS_BORDER

	sendmsg hlink1, $30, 0, hFontn

	oncmd gosub *command, $111      //マウスクリック時のウィンドウメッセージ
	oncmd gosub *wmsetcursor, $20   //WM_SETCURSOR

	/***MAIN_exここまで***/



/*******************************************************************************
								SUB2_ex
********************************************************************************/

	bgscr SUB2_ex, 1100, 625, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hwindow

	font "メイリオ", 16 : objmode 2, 0

	pos 20, 0
	mes "bgscr命令による専用画面遷移動作実験"
	x3 = ginfo(14)
	mes "こちらはSUB2_exです。", 1
	objsize 100, 25

	pos x3 + 30, 0
	button gosub "MAIN_exへ", *onmain

	pos x3 + 30 + 160, 0
	button gosub "SUB1_exへ", *onsub1



	/***タブコントロール***/
	pos 50, 100
	My_CreateTab 600, 300, hTab_ex

	InsertTab tabname(0), tabid1, 0, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ1 です。"


	InsertTab tabname(1), tabid2, 1, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ2 です。"


	InsertTab tabname(2), tabid3, 2, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ3 です。"


	InsertTab tabname(3), tabid4, 3, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ4 です。"


	InsertTab tabname(4), tabid5, 4, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ5 です。"


	gsel tabid1, 1


	gsel SUB2_ex
	oncmd gosub *notify_ex, WM_NOTIFY
	/***SUB2_exここまで***/

	gsel MASTER_ex, 1
	gsel SUB1_ex, 1

	oncmd 1
	stop

//############################## < 以下処理系 > #################################


*command
	hL1 = lparam
	if (hL1 == hlink1) {exec link, 16}
	return

*wmsetcursor
	if (lparam & $FFFF) != 1 : return
	// オブジェクトの左上XY右下XYの座標位置を取得
	GetWindowRect hlink1, varptr(RECT)
	// 取得したオブジェクトの座標位置とカーソルの座標位置を比較
	PtInRect varptr(RECT), ginfo(0), ginfo(1)
	if stat {
		if oncursor : return
		InvalidateRect hlink1, 0, 0
		oncursor = 1
		return
	}
	else {
		if oncursor = 0 : return
		InvalidateRect hlink1, 0, 0
		oncursor = 0
		return
	}
	return



/*********ウィンドウ切り替え処理*********/

*onmain
	gsel SUB1_ex, -1 : gsel SUB2_ex, -1
	gsel MAIN_ex, 1
	return

*onsub1
	gsel MAIN_ex, -1 : gsel SUB2_ex, -1
	gsel SUB1_ex, 1
	return

*onsub2
	gsel SUB1_ex, -1 : gsel MAIN_ex, -1
	gsel SUB2_ex, 1
	return
/******ウィンドウ切り替え処理ここまで*****/

//タブコントロールの切り替え
*notify_ex
	dupptr nmhdr, lparam, 12, 4
	if (nmhdr(2) == -551) {
		My_ChangeTab tabid1, hTab_ex, SUB2_ex
	}
	return

//選択したリストアイテムの取得
*getitem
	gsel MAIN_ex
	sdim infonoteC, 1024 : sdim infotext1, 1024
	sdim infonoteG, 1024 : sdim infotext2, 1024
	sdim infonoteR, 1024 : sdim infotext3, 1024

	mygetitem hLVcsouko, 6, infonoteC
	mygetitem hLVgsouko, 6, infonoteG
	mygetitem hLVrsouko, 6, infonoteR

	infotext1 += "\n\n---CPU---" + "\n" + infonoteC
	infotext2 += "\n\n---GPU---" + "\n" + infonoteG
	infotext3 += "\n\n---ROM---" + "\n" + infonoteR
	kounyu = "\n\n\t\t以上を購入しますか?"

	dialog "購入リスト" + infotext1 + infotext2 + infotext3 + kounyu, 0, "購入確認"

	return

//並べ替え
*listnotify
	dupptr nmhdr, lparam, 12, 4 : id = nmhdr(0)
	if (nmhdr(2) = LVN_COLUMNCLICK) {
		dupptr nmlv, lparam, 40, 4 : inde = nmlv(4)

		swi = swi * -1
		if (swi == -1) {sqsc = " DESC;"} else {sqsc = " ASC;"}
		/***描画先・操作先***/
		gsel MAIN_ex

		sql_open dbfiles
		sql_q "BEGIN;"
		switch id
			/***CPUの処理***/
			case hLVcsouko
				sdim cpu, 2048 : sdim data_cpu, 1024
				sql_q "SELECT * FROM MyCPU ORDER BY " + cpu_culm(inde) + sqsc
				myindata cpu_cunt, c_culm_num, cpu_culm, cpu
				/***今あるリストアイテムの削除***/
				dellv hLVcsouko
				insqlitem hLVcsouko, cpu, cpu_cunt, c_culm_num
				swbreak

			/***GPU***/
			case hLVgsouko
				sdim gpu, 2048 : sdim data_gpu, 1024
				sql_q "SELECT * FROM MyGPU ORDER BY " + gpu_culm(inde) + sqsc
				myindata gpu_cunt, g_culm_num, gpu_culm, gpu
				dellv hLVgsouko
				insqlitem hLVgsouko, gpu, gpu_cunt, g_culm_num
				swbreak

			/***ROM***/
			case hLVrsouko
				sdim rom, 2048 : sdim data_rom, 1024
				sql_q "SELECT * FROM MyROM ORDER BY " + rom_culm(inde) + sqsc
				myindata rom_cunt, r_culm_num, rom_culm, rom
				dellv hLVrsouko
				insqlitem hLVrsouko, rom, rom_cunt, r_culm_num
				swbreak
		swend
		sql_q "COMMIT;"
		sql_close
	}
	return
#endif

/*******************************************************************************
		SQLite DB Table Schema Infomation for HSP3.7beta4 (SQLele)
		対象となるDBファイル内のテーブルスキーマを確認するスクリプト
********************************************************************************/
#if 0
	#packopt name "SQLite DB TSI"
	#packopt type 0
	#packopt hide 1

	#include "user32.as"
	#include "hsp3util.as"
	#include "hsedsdk.as"
	#include "sqlele.hsp"
	#include "00_module.hsp"
	#include "00_mylv.hsp"


	sdim wintitle, 126, 2
	wintitle(0) = "SQLite DB Table Schema Information for HSP" + myhspver(hspver) + " (Presented by SQLele)"
	wintitle(1) = "SQLite DB TSI - Schema"

	// オブジェクトハンドル
	// hObjhwnd(0) : ファイルパス表示
	// hObjhwnd(1) : OPENボタン
	// hObjhwnd(2) : schma表示
	// hObjhwnd(3) : リストビュー
	dim hObjhwnd, 4  :  hObjhwnd = 0, 0, 0, 0

	dim i, 3  :  i = 0, 0, 0            // ループ文カウンタ
	dim hogehoge, 2
	hogehoge = 0, 0                     // 多目的変数

	sdim dbschema, 4096
	sdim sqschema, 4096
	sdim nbuf1, 1024
	sdim nbuf2, 1024
	sdim dbfiles

	lvcolw = 45, 170, 100, 70, 30



//----------------------------------------------------------
//メインウィンドウ
//----------------------------------------------------------

	screen 0, 650, 500, 2  :  title wintitle(0)
	font "MyricaM M", 14 : mref BMSCR, 67 : hFont = BMSCR(38) : objmode 2, 1


	pos 5, 10  :  mes "DataBase File"

	/***  ファイルパス  ***/
	pos 5,
	winobj "static", dbfiles, , $50000000 | SS_SUNKEN, 500, 18
	hObjhwnd(0) = objinfo(stat, 2)

	/***  OPENボタン  ***/
	objsize 90, 25
	pos 520, 20
	button gosub "OPEN", *OPEN
	hObjhwnd(1) = objinfo(stat, 2)

	/***  ListView  ***/
	pos 2,
	mylv 500, 400, hObjhwnd(3)
	sendmsg hObjhwnd(3), 0x1036, , 0x00000020 | 0x00000010


//--------------------------------------
	/*** Create Dummy DB ***/
	sql_open dbfiles
	sql_q "SELECT * FROM sqlite_master;", dbschema
	incolm hObjhwnd(3), dbschema, length(dbschema), lvcolw
	sql_close
//--------------------------------------




//----------------------------------------------------------
//サブウィンドウ
//----------------------------------------------------------

	screen 1, 650, 500, 2  :  title wintitle(1)

	/***  schema  ***/
	pos 5, 5
	mesbox sqschema, 640, 490, 0
	hObjhwnd(2) = objinfo(stat, 2)



	/***フォントの適用***/
	sendmsg hObjhwnd(0), $30, hFont
	sendmsg hObjhwnd(1), $30, hFont
	sendmsg hObjhwnd(2), $30, hFont
	sendmsg hObjhwnd(3), $30, hFont

	gsel 1, 1
	gsel 0, 1
	stop


/***********************************************************/


// 表示処理
*OPEN

	/***  ファイルオープン  ***/
	dialog "db", 16, "DataBase"
	if (stat = 0) {return} else {dbfiles = refstr}

	// 拡張子確認
	if (getpath(refstr, 2) != ".db") {
		dialog "This file is not DataBase file!", 1, "FileError"
		sdim dbfiles
		return
	}

	// .dbファイルのパス表示
	sendmsg hObjhwnd(0), $C, 0, dbfiles
	logpri dbfiles

	sdim sqschema, 4096                 // 変数初期化
	sdim nbuf1, 1024
	sdim nbuf2, 1024
	hogehoge = 0, 0


	/*** DB OPEN ***/
	sql_open dbfiles
	sql_q "SELECT * FROM sqlite_master;", dbschema
	dellv hObjhwnd(3), 0, 2             // リストビュー初期化
	insqlitem hObjhwnd(3), dbschema     // リストビューに全データ入力
	dellv hObjhwnd(3), 4, 1             // カラムインデックス4 "sqlカラム"の削除
	sql_close
	/*** DB CLOSE ***/


	// 削除した "sqlカラム"のレコードのみバッファーに保存
	repeat length2(dbschema) - 1
		sqschema += "\n" + dbschema(4, cnt)
	loop

	notesel sqschema

	repeat notemax
		noteget nbuf1, cnt
		if (nbuf1 == "") {notedel cnt}
	loop
	sdim nbuf1, 1024

	logpri sqschema

	strrep sqschema, "\n", ""
	strrep sqschema, "\t", ""
	strrep sqschema, "CREATE", "\n\nCREATE"

	logpri sqschema

	logpri notefind(")", 2)
	if (notefind(")", 2) != -1) {
		noteget nbuf1, notefind(")", 2)
		strrep nbuf1, "(", "(\n"
		strrep nbuf1, ")", "\n)"
		strrep nbuf1, "[", "\n["
	}

	logpri nbuf1
	logpri sqschema

	strrep sqschema, " AS ", "\n\tAS "
	strrep sqschema, " FROM", "\n\tFROM"
	strrep sqschema, " WHERE", "\n\tWHERE"
	strrep sqschema, " ORDER", "\n\tORDER"
	logpri sqschema

	split nbuf1, "\n", nbuf1
	i(0) = stat
	logpri "nbuf1↓"
	foreach nbuf1
		logmes nbuf1(cnt)
	loop
	foreach i(0)
		noteadd nbuf1(i(0) - 1) + "\n", 0, 0
		i(0)--
	loop

	logpri sqschema

	noteunsel

	sendmsg hObjhwnd(2), $C, 0, sqschema
	return

#endif


/*##############################################################################
						hspバージョン確認
###############################################################################*/
#if 0
	#include "hsedsdk.as"
	#include "00_module.hsp"
	#include "00_mylv.hsp"

	screen 0, 500, 300
	font "MyricaM M", 16
	mref BMSCR, 67 : hFont = BMSCR(38)

	sdim d1  :  sdim d2, 20, 8  :  dim d3  :  sdim aa  :  sdim bb
	hsed_cnvverstr __hspver__  :  aa += refstr
	hsed_cnvverstr hspver      :  bb += refstr

	d1 = "macro", "int", "hsed_cnvverstr", "myhspver()"
	d2(0) = "__hspver__"
	d2(1) = str(__hspver__)
	d2(2) = aa
	d2(3) = myhspver (__hspver__)
	d2(4) = "hspver"
	d2(5) = str(hspver)
	d2(6) = bb
	d2(7) = myhspver (hspver)
	d3 = 110, 75, 140, 110

	mylv 440, 120, id_lv, hLv
	incolm id_lv, d1, 4, d3
	inlvitem id_lv, d2, 2, 4

	sendmsg objinfo(id_lv, 2), $30, hFont

#endif

//##############################################################################
//								キーコード確認
//##############################################################################
#if 0
#packopt name "Key Get"
	screen 0, 350, 200
	sdim moni : dim number, 5
	pos 10, 10
	mesbox moni, 200, 120, 0
	id_moni = stat

*inkey
	wait 16
	getkey ctrl, 17
	getkey number(0), 49
	getkey number(1), 50
	getkey number(2), 51
	getkey number(3), 52
	getkey number(4), 53

	moni = {"
	[ aa ] (bb)

	文字コード       [ ii ]
	(iparam)
	仮想キーコード  [ ww ] ( wx )
	(wparam)
	"}
	strrep moni, "aa", strf("%c", iparam)
	strrep moni, "bb", str(lparam >> 30 & 1)
	strrep moni, "ii", str(iparam)
	strrep moni, "ll", str(lparam)
	strrep moni, "ww", str(wparam)
	strrep moni, "wx", strf("%X", wparam)

	notesel moni
	if (ctrl == 1) & (number(0) == 1) : noteadd "\n Ctrl + 1"
	if (ctrl == 1) & (number(1) == 1) : noteadd "\n Ctrl + 2"
	if (ctrl == 1) & (number(2) == 1) : noteadd "\n Ctrl + 3"
	if (ctrl == 1) & (number(3) == 1) : noteadd "\n Ctrl + 4"
	if (ctrl == 1) & (number(4) == 1) : noteadd "\n Ctrl + 5"
	objprm id_moni, moni
	noteunsel
	goto *inkey
#endif