/*******************************************************************************
								Master Window
********************************************************************************/
	screen ID_WIN_MASTER, 800, 500, 2
	hWin_master = hwnd
	title titlemes
	font "メイリオ", 16 : objmode 2, 1 : oncmd 0
	//HSPのBMSCR構造体からフォントハンドルを取得
	mref BMSCR, 67 : hFont = BMSCR.38
/*************<<  メニューバーの設置  >>*************/

	#define CMD_OPEN 1
	#define CMD_SAVE 2
	#define CMD_QUIT 3
	#define CMD_INFO 4
	#define CMD_START 5
	/******サブメニューの設定******/
	// メニューバー >> ファイル V
	newmenu hfilemenu, 1
	addmenu hfilemenu, "Open(&O)", CMD_OPEN
	addmenu hfilemenu, "Save(&S)", CMD_SAVE
	addmenu hfilemenu, "Quit(&Q)", CMD_QUIT
	// メニューバー >> ヘルプ V
	newmenu hhelpmenu, 1
	addmenu hhelpmenu, "Information(&I)", CMD_INFO
	addmenu hhelpmenu, "STARTwindow", CMD_START
	/******メインメニューの設定******/
	// ファイル(F) | ヘルプ(H)
	newmenu hmenu, 0
	addmenu hmenu, "File(&F)", hfilemenu, 0x10
	addmenu hmenu, "Help(&H)", hhelpmenu, 0x10
	applymenu hmenu // 設定したメニューバーを適用



/*******************************************************************************
								Opening Window
********************************************************************************/
	bgscr ID_WIN_OPEN, 800, 500, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hWin_master

	Mycolor 0.4, 0.7, 0.85, 0
	boxf 10, 10, 790, 480

	font "メイリオ", 40, 1, 4
	pos 120, 40
	Mycolor 1, 0.5, 0.75, 1 //輪郭線
	Mycolor 0.95, 0.95, 0.95, 0 //文字色
	mes "PC_Careing Diary ver" + ver, 4

	font "メイリオ", 40, 1, 2
	pos 120, 40
	Mycolor 0, 0, 0, 1 //輪郭線
	Mycolor 0.95, 0.95, 0.95, 0 //文字色
	mes "PC_Careing Diary ver" + ver, 4

	pos 300, 200 : objsize 200, 40, 60 : font "けいふぉんと", 16 : objmode 2, 1

	button gosub "新しく始める", *OP新規
	id_opB0 = stat
	button gosub "続きから始める", *OPload
	id_opB1 = stat

	font "メイリオ", 13
	//HSPのBMSCR構造体からフォントハンドルを取得
	mref BMSCR3, 67 : hFont3 = BMSCR3.38




/*******************************************************************************
								Main Window
********************************************************************************/
	bgscr ID_WIN_MAIN, 800, 500, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hWin_master

	font "メイリオ", 16 : objmode 2, 1
/*******************<<  ウィンドウオブジェクトの設置  >>********************/

	/***ゲーム内時間***/
	hx = 580 : hy = 15
	pos hx, hy
	winobj "static", "ゲーム内時間を表記します", , $50000000 | $1 | $800000, 200, 28
	hStatic_h = objinfo(stat, 2)

	/***ステータス項目表示***/
	mx = 20 : my = 35
	pos mx, my
	winobj "static", " C P U\n G P U\n R O M\n S S D\n F a n\n 温 度\n Power", , $50000000 | $800000, 170, 170
	hStatic_i = objinfo(stat, 2)

	/***ステータス数値表示***/
	pos mx + 58, my + 1
	winobj "static", "", , $50000000 | $800000 | $2, 70, 169
	hStatic_m = objinfo(stat, 2)

	/***ステータス単位表示(変動用)***/
	pos mx + 126, my + 50
	winobj "static", " B\n B", , $50000000 | $800000, 40, 50
	hStatic_u = objinfo(stat, 2)

	/***ステータス単位表示(固定用)***/
	pos mx + 126, my + 96
	winobj "static", " rpm\n deg\n %", , $50000000 | $800000, 40, 70
	hStatic_u2 = objinfo(stat, 2)

/*******************************************************************************/

	/***タブコントロール***/
	pos 0, 240
	My_CreateTab 800, 240, hTab_main

//==========================================================
	InsertTab "Tools", ID_TAB_MOTI, 0, hTab_main
	font "メイリオ", 15 : objmode 2, 0

	pos 0, 0
	mesbox motimono, 794, 210, 0
	id_motimono = stat

//==========================================================
	InsertTab "Arsenal", ID_TAB_SOUKO, 1, hTab_main

	pos 5, 0
	My_CreateLv 794, 210, id_soukomoni, hLVsouko

//==========================================================
	InsertTab "Logs", ID_TAB_LOG, 2, hTab_main

	pos 0, 0
	mesbox logmoni, 794, 210, 0
	id_logmoni = stat

//==========================================================

	gsel ID_TAB_MOTI, 1 : gsel ID_WIN_MAIN, -1

	/******フォントの反映******/
	sendmsg hStatic_h, $30, hFont
	sendmsg hStatic_i, $30, hFont
	sendmsg hStatic_m, $30, hFont
	sendmsg hStatic_u, $30, hFont
	sendmsg hStatic_u2, $30, hFont
	/******フォントの反映******/
	sendmsg hTab_main, $30, hFont2
	sendmsg hLVsouko, $30, hFont3

//--------------------------------------レイヤーオブジェクトlay0
	pos 0, 0
	layerobj 800, 200, 3, *lay, 0
	layid0 = stat

//--------------------------------------再開ボタン
	pos hx + 90, hy + 50
	objsize 110, 30
	button gosub "START", *ope
	id_opebut = stat
	SetWindowLong objinfo(id_opebut, 2), -16, $50000003 | $1000





/*******************************************************************************
								Amazoon
********************************************************************************/

	bgscr ID_WIN_AMAZ, 800, 500, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hWin_master

	pos 5, 5
	My_CreateTab 750, 230, hTabOMI
	listx = 550
	listy = 160


	InsertTab "C P U", ID_OMTAB_CPU, 5, hTabOMI

	pos 2, 2

	My_CreateLv listx, listy, id_LVCPU, hLVCPU
	My_InsertColumn id_LVCPU, col_clis, col_cnum, column_w
	My_InsertLvItem id_LVCPU, cpu, rec_cnum, col_cnum
	oncmd gosub *notify, WM_NOTIFY


	InsertTab "G P U", ID_OMTAB_GPU, 6, hTabOMI

	pos 2, 2

	My_CreateLv listx, listy, id_LVGPU, hLVGPU
	My_InsertColumn id_LVGPU, col_glis, col_gnum, column_w
	My_InsertLvItem id_LVGPU, gpu, rec_gnum, col_gnum
	oncmd gosub *notify, WM_NOTIFY


	InsertTab "R O M", ID_OMTAB_ROM, 7, hTabOMI

	pos 2, 2

	My_CreateLv listx, listy, id_LVROM, hLVROM
	My_InsertColumn id_LVROM, col_rlis, col_rnum, column_w
	My_InsertLvItem id_LVROM, rom, rec_rnum, col_rnum
	oncmd gosub *notify, WM_NOTIFY

	InsertTab "S S D", ID_OMTAB_SSD, 8, hTabOMI
	pos 2, 2
	My_CreateLv listx, listy, id_LVSSD, hLVSSD
	oncmd gosub *notify, WM_NOTIFY

	InsertTab "F A N", ID_OMTAB_FAN, 9, hTabOMI
	pos 2, 2
	My_CreateLv listx, listy, id_LVFAN, hLVFAN
	oncmd gosub *notify, WM_NOTIFY

	InsertTab "Power", ID_OMTAB_POW, 10, hTabOMI
	pos 2, 2
	My_CreateLv listx, listy, id_LVPOW, hLVPOW
	oncmd gosub *notify, WM_NOTIFY

	gsel ID_OMTAB_CPU, 1
	oncmd gosub *notify, WM_NOTIFY //タブ項目切り替え処理時のメッセージ
	gsel ID_WIN_AMAZ

	/******フォントの反映******/
	sendmsg hTabOMI, $30, hFont3





/*******************************************************************************
								Setting
********************************************************************************/

	bgscr ID_WIN_SETI, 800, 500, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hWin_master

	font "メイリオ", 12 : objmode 2, 0

	pos 20, 20 : objsize 115, 20
	chkbox "AutoSave OFF", setting(0, 0)
	id_savebut = stat : hButton = objinfo(id_savebut, 2)

		//==================================ゲーム速度の設定
	pos 20, 60 : objsize 60, 19
	mes "Game Speed"

		//----------------------------------chkbox
	pos 20, 90
	chkbox "Slowest", setting(1, 0)
	id_chkosoi = stat : hChkbox1 = objinfo(id_chkosoi, 2)
	sendmsg hChkbox1, $F4, $9

		//----------------------------------chkbox
	pos 110, 90
	chkbox "Normal", setting(2, 0)
	id_chkfutu = stat : hChkbox2 = objinfo(id_chkfutu, 2)
	sendmsg hChkbox2, $F4, $9

		//----------------------------------chkbox
	pos 200, 90
	chkbox "Fastest", setting(3, 0)
	id_chkhaya = stat : hChkbox3 = objinfo(id_chkhaya, 2)
	sendmsg hChkbox3, $F4, $9

	//------------------適用ボタン[ID=5]
	pos 700, 170 : objsize 75, 28
	button gosub "Active", *tekiou




/*******************************************************************************
							Infomation Window
********************************************************************************/
	screen ID_WIN_INFO, 650, 450, 6 : title "Information ver" + ver
	font "メイリオ", 16 : objmode 2, 0
	pos 10, 15 : mes "N A M E\nVERSION\nHSP ver\nAUTHOR\nU  R  L", 1
	pos 100,
	repeat 5
		mes infa(cnt)
	loop
//==============================================================================
//						<<  オブジェクトの設置  >>
//==============================================================================
//----------------------------------------------------------サブmesbox設置[ID=0]
	font "メイリオ", 14 : objmode 2, 1
	//HSPのBMSCR構造体からフォントハンドルを取得
	mref BMSCR2, 67 : hFont2 = BMSCR2.38
	pos 0, 140
	mes "  UPDATE HISTORY  "
	mesbox infa2, 650, 262, 0
//----------------------------------------------------------サブbutton設置[ID=1]
	pos 525, 426 : objsize 125,
	button gosub "Close", *subw_exi



//##############################################################################

	gsel ID_WIN_MAIN, 0
	oncmd gosub *notify, WM_NOTIFY //タブ項目切り替え処理時のメッセージ

	gsel ID_WIN_OPEN, 1
	gsel ID_WIN_MASTER, 1
	oncmd 1
	oncmd gosub *OnCommand, WM_COMMAND   //メニューバー
	onexit gosub *exit	//終了ボタン