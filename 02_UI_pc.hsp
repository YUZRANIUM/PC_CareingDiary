//							メインウィンドウ[winID=0]
	screen ID_WIN_MAIN, 800, 500, 6 : gsel ID_WIN_MAIN, -1 : title titlemes
	font "メイリオ", 16 : objmode 2, 1 : oncmd 0
	//HSPのBMSCR構造体からフォントハンドルを取得
	mref BMSCR, 67 : hFont = BMSCR.38
/*************<<  メニューバーの設置  >>*************/

	#define CMD_OPEN 1
	#define CMD_SAVE 2
	#define CMD_QUIT 3
	#define CMD_INFO 4
	/******サブメニューの設定******/
	// メニューバー >> ファイル V
	newmenu hfilemenu, 1
	addmenu hfilemenu, "開く(&O)", CMD_OPEN
	addmenu hfilemenu, "保存(&S)", CMD_SAVE
	addmenu hfilemenu, "終了(&Q)", CMD_QUIT
	// メニューバー >> ヘルプ V
	newmenu hhelpmenu, 1
	addmenu hhelpmenu, "このソフトについて(&I)", CMD_INFO
	/******メインメニューの設定******/
	// ファイル(F) | ヘルプ(H)
	newmenu hmenu, 0
	addmenu hmenu, "ファイル(&F)", hfilemenu, 0x10
	addmenu hmenu, "ヘルプ(&H)", hhelpmenu, 0x10
	applymenu hmenu // 設定したメニューバーを適用

/*******************<<  ウィンドウオブジェクトの設置  >>********************/

	hx = 580 : hy = 15
	/***ゲーム内時間***/
	pos hx, hy
	winobj "static", "ゲーム内時間を表記します", , $50000000 | $1 | $800000, 200, 28
	//オブジェhWnd (ゲーム内時間)
	hStatic_h = objinfo(stat, 2)

	mx = 20 : my = 35
	/***ステータス項目表示***/
	pos mx, my
	winobj "static", " C P U\n G P U\n メモリ\n S S D\n F a n\n 温 度\n Power", , $50000000 | $800000, 170, 170
	//オブジェhWnd (ステータス項目)
	hStatic_i = objinfo(stat, 2)


	/***ステータス数値表示***/
	pos mx + 58, my + 1
	winobj "static", "", , $50000000 | $800000 | $2, 70, 169
	//オブジェhWnd (ステータス数値)
	hStatic_m = objinfo(stat, 2)


	/***ステータス単位表示(変動用)***/
	pos mx + 126, my + 50
	winobj "static", " B\n B", , $50000000 | $800000, 40, 50
	//オブジェhWnd (ステータス単位変動用)
	hStatic_u = objinfo(stat, 2)


	/***ステータス単位表示(固定用)***/
	pos mx + 126, my + 96
	winobj "static", " rpm\n deg\n %", , $50000000 | $800000, 40, 70
	//オブジェhWnd (ステータス単位固定)
	hStatic_u2 = objinfo(stat, 2)

/*******************************************************************************/

	/***タブコントロール***/
	pos 0, 240
	My_CreateTab 800, 240, hTab_main

//==========================================================[winID=3]
	InsertTab "持ち物", ID_TAB_MOTI, 0, hTab_main
	font "メイリオ", 15 : objmode 2, 0
	pos 0, 0
	mesbox motimono, 794, 210, 0
	//オブジェID (motimono)
	id_motimono = stat


//==========================================================[winID=4]
	InsertTab "倉  庫", ID_TAB_SOUKO, 1, hTab_main
	pos 5, 0
	My_CreateListView 794, 210, id_soukomoni, hLVsouko
	//My_InsertColumn id_soukomoni, lis, 6, clum_w, 0





//==========================================================[winID=5]
	InsertTab "お  店", ID_TAB_OMISE, 2, hTab_main
	gsel ID_TAB_OMISE
	pos 5, 5
	My_CreateTab 750, 230, hTabOMI
	listx = 550
	listy = 160
	InsertTab "C P U", ID_OMTAB_CPU, 5, hTabOMI
	pos 2, 2
	My_CreateListView listx, listy, id_LVCPU, hLVCPU
	//My_InsertColumn id_LVCPU, lis, 6, clum_w, 0
	//My_InsertChItem id_LVCPU, syou, 1, 6

	InsertTab "G P U", ID_OMTAB_GPU, 6, hTabOMI
	pos 2, 2
	My_CreateListView listx, listy, id_LVGPU, hLVGPU
	//My_InsertColumn id_LVGPU, lis, 6, clum_w, 0

	InsertTab "メモリ", ID_OMTAB_ROM, 7, hTabOMI
	pos 2, 2
	My_CreateListView listx, listy, id_LVROM, hLVROM
	//My_InsertColumn id_LVROM, lis, 6, clum_w, 0
	//My_InsertChItem id_LVROM, syou, 2, 6

	InsertTab "S S D", ID_OMTAB_SSD, 8, hTabOMI
	pos 2, 2
	My_CreateListView listx, listy, id_LVSSD, hLVSSD
	//My_InsertColumn id_LVSSD, lis, 6, clum_w, 0

	InsertTab "F A N", ID_OMTAB_FAN, 9, hTabOMI
	pos 2, 2
	My_CreateListView listx, listy, id_LVFAN, hLVFAN
	//My_InsertColumn id_LVFAN, lis, 6, clum_w, 0

	InsertTab "Power", ID_OMTAB_POW, 10, hTabOMI
	pos 2, 2
	My_CreateListView listx, listy, id_LVPOW, hLVPOW
	//My_InsertColumn id_LVPOW, lis, 6, clum_w, 0


	gsel ID_OMTAB_CPU, 1 : gsel ID_TAB_OMISE
	oncmd gosub *notify, $4E //タブ項目切り替え処理時のメッセージ
	gsel ID_WIN_MAIN


//==========================================================[winID=6]
	InsertTab "ロ  グ", ID_TAB_LOG, 3, hTab_main
	pos 0, 0
	mesbox logmoni, 794, 210, 0
	//オブジェID (logmoni)
	id_logmoni = stat


//==========================================================[winID=7]
	InsertTab "設  定", ID_TAB_SETTEI, 4, hTab_main
	font "メイリオ", 12 : objmode 2, 0

	pos 20, 20 : objsize 115, 20
	chkbox "オートセーブOFF", opt(0, 0)
	id_savebut = stat : hButton = objinfo(id_savebut, 2)
	savebut_text = "オートセーブOFF", "オートセーブON"

	//==================================ゲーム速度の設定
	pos 20, 60 : objsize 60, 19
	mes "ゲーム速度"
	//----------------------------------chkbox
	pos 20, 90
	chkbox "遅  め", opt(1, 0)
	id_chkosoi = stat : hChkbox1 = objinfo(id_chkosoi, 2)
	sendmsg hChkbox1, $F4, $9
	//----------------------------------chkbox
	pos 110, 90
	chkbox "ふつう", opt(2, 0)
	//オブジェID (設定ゲーム速度)
	id_chkfutu = stat
	//オブジェhWnd (設定ゲーム速度)
	hChkbox2 = objinfo(id_chkfutu, 2)
	sendmsg hChkbox2, $F4, $9
	//----------------------------------chkbox
	pos 200, 90
	chkbox "早  め", opt(3, 0)
	//オブジェID (設定ゲーム速度)
	id_chkhaya = stat
	//オブジェhWnd (設定ゲーム速度)
	hChkbox3 = objinfo(id_chkhaya, 2)
	sendmsg hChkbox3, $F4, $9

	//------------------適用ボタン[ID=5]
	pos 700, 170 : objsize 75, 28
	button gosub "適  用", *tekiou

//==========================================================

	gsel ID_OMTAB_CPU, 1 : gsel ID_WIN_MAIN, 0

	/******フォントの反映******/
	sendmsg hStatic_h, $30, hFont
	sendmsg hStatic_i, $30, hFont
	sendmsg hStatic_m, $30, hFont
	sendmsg hStatic_u, $30, hFont
	sendmsg hStatic_u2, $30, hFont

//--------------------------------------レイヤーオブジェクトlay0
	pos 0, 0
	layerobj 800, 200, 4, *lay, 0
	//オブジェID (レイヤーオブジェクト)
	layid0 = stat

//--------------------------------------再開ボタン
	pos hx + 90, hy + 50
	objsize 110, 30
	button gosub "スタート", *ope
	//オブジェID (再開ボタン)
	id_opebut = stat
	SetWindowLong objinfo(id_opebut, 2), -16, $50000003 | $1000

//##############################################################################
//					情報ウィンドウ[winID=1]
//##############################################################################

	screen ID_WIN_INFO, 650, 450, 6 : gsel ID_WIN_INFO, -1 : title "このソフトについて ver" + ver
	font "メイリオ", 16 : objmode 2, 0

	pos 10, 15 : mes "N A M E\nVERSION\nHSP ver\nAUTHOR\nU  R  L", 1
	pos 100,
	repeat 5
		mes infa(cnt)
	loop
//==============================================================================
//			<<  オブジェクトの設置  >>
//==============================================================================
//----------------------------------------------------------サブmesbox設置[ID=0]
	font "メイリオ", 14 : objmode 2, 1
	//HSPのBMSCR構造体からフォントハンドルを取得
	mref BMSCR2, 67 : hFont2 = BMSCR2.38

	pos 0, 140
	mes "  更新履歴  "
	mesbox infa2, 650, 262, 0
//----------------------------------------------------------サブbutton設置[ID=1]
	pos 525, 426 : objsize 125,
	button gosub "閉じる", *subw_exi



//##############################################################################
//					オープニング画面[winID=2]
//##############################################################################

	screen ID_WIN_OPEN, 800, 500, 6 : gsel ID_WIN_OPEN, -1 : title titlemes
	objmode 2, 1

	mycolor 0.4, 0.7, 0.85, 0
	boxf 10, 10, 790, 480

	font "メイリオ", 40, 1, 4
	pos 200, 40
	mycolor 1, 0.5, 0.75, 1 //輪郭線
	mycolor 0.95, 0.95, 0.95, 0 //文字色
	mes "PC育成日記ver" + ver, 4

	font "メイリオ", 40, 1, 2
	pos 200, 40
	mycolor 0, 0, 0, 1 //輪郭線
	mycolor 0.95, 0.95, 0.95, 0 //文字色
	mes "PC育成日記ver" + ver, 4

	pos 300, 200 : objsize 200, 40, 60 : font "けいふぉんと", 16
	button gosub "新しく始める", *OP新規
	//オブジェID (OP画面新規ボタン)
	id_opB0 = stat
	button gosub "続きから始める", *OPロード
	//オブジェID (OP画面ロードボタン)
	id_opB1 = stat

	/*******************************************************************************/

	font "メイリオ", 13
	//HSPのBMSCR構造体からフォントハンドルを取得
	mref BMSCR3, 67 : hFont3 = BMSCR3.38

	gsel ID_WIN_MAIN, 0
	/******フォントの反映******/
	sendmsg hTab_main, $30, hFont2
	sendmsg hLVomise, $30, hFont3
	sendmsg hLVsouko, $30, hFont3
	sendmsg hTabOMI, $30, hFont3

	oncmd 1
	oncmd gosub *OnCommand, WM_COMMAND   //メニューバー
	oncmd gosub *notify, $4E //タブ項目切り替え処理時のメッセージ
	onexit gosub *exit	//終了ボタン
	gsel ID_WIN_OPEN, 1