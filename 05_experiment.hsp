// INFO ************************************************
// FileName : 05_experiment.hsp
// Date   : 2022/10/06
// Author : YUZURANIUM（ゆずらにうむ）
// URL    : https://twitter.com/YUZRANIUM
//******************************************************
#packopt name "05_ver016ex"
#packopt type 0

	hspv = strf("%.3f", double(strf("%4x", hspver)) / 1000)
	title " 05_ver0.1.6experiment  for  HSP " + hspv
	sdim logfile_ex, 2048
	notesel logfile_ex
	noteload "05_ver000ex.txt" : noteunsel

#if 1
/*##############################################################################
							画面切り替え機能実験
###############################################################################*/
	#include "user32.as"
	#include "gdi32.as"
	#include "sqlele.hsp"
	#include "00_module.hsp"
	/***ウィンドウID***/
	#enum MASTER_ex = 0
	#enum MAIN_ex
	#enum SUB1_ex
	#enum SUB2_ex

	#enum tabid1
	#enum tabid2
	#enum tabid3
	#enum tabid4
	#enum tabid5
/*******************************************************************************
								MASTER_ex
********************************************************************************/

	screen MASTER_ex, 1100, 650, 2 : font "メイリオ", 16
	hwindow = hwnd
	mref BMSCRn, 67 : hFontn = BMSCRn.38

	sdim tabname, 64 : sdim mousmoni, 64
	tabname = "Tab1", "Tab2", "Tab3", "Tab4", "Tab5"
	sdim cpu, 2048 : sdim data_cpu, 1024 : sdim cpu_culm, 64 : dim c_culm_num
	sdim gpu, 2048 : sdim data_gpu, 1024 : sdim gpu_culm, 64 : dim g_culm_num
	sdim rom, 2048 : sdim data_rom, 1024 : sdim rom_culm, 64 : dim r_culm_num
	cpu_culm = "" : gpu_culm = "" : rom_culm = "" : all_clum = ""

	Tyuc = 1 : Tyug = 1 : Tyur = 1
	c_clum_w = 50, 55, 75, 80, 80, 80
	g_clum_w = 50, 55, 75, 80, 80, 80
	r_clum_w = 50, 55, 75, 80, 80, 80
	f = "Syouhin.db"

	sql_open f
	sql_q "BEGIN;"

	sql_q "SELECT * FROM MyCPU;"
	//レコードの数
	cpu_cunt = stat
	//カラムの数
	c_culm_num = length(tmparr)
	//カラムの一覧リスト
	cpu_culm = sql_collist() : split cpu_culm, ",", cpu_culm
	//DBのレコードを文字列型1次元配列変数に格納する
	Datainput cpu_cunt, c_culm_num, cpu_culm, cpu

	sql_q "SELECT * FROM MyGPU;"
	gpu_cunt = stat
	gpu_culm = sql_collist() : split gpu_culm, ",", gpu_culm
	g_culm_num = length(tmparr)
	Datainput gpu_cunt, g_culm_num, gpu_culm, gpu

	sql_q "SELECT * FROM MyROM;"
	rom_cunt = stat
	rom_culm = sql_collist() : split rom_culm, ",", rom_culm
	r_culm_num = length(tmparr)
	Datainput rom_cunt, r_culm_num, rom_culm, rom

	sql_q "COMMIT;"
	sql_close

	/***MASTERここまで***/


/*******************************************************************************
								MAIN_ex
********************************************************************************/

	bgscr MAIN_ex, 1100, 625, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hwindow

	font "メイリオ", 14 : objmode 2, 0

	pos 10, 0 : mes f : x2 = ginfo(14)
	objsize 150, 25 : pos x2 + 20, 0

	button gosub "Your Get List Items", *getitem
	id_butget = stat:hButget = objinfo_hwnd(id_butget)

	pos x2 + 300, 0
	button gosub "SUB1_exへ", *onsub1
	id_gowin = stat : hButnew = objinfo_hwnd(id_gowin)

	pos x2 + 300 + 180, 0
	button gosub "SUB2_exへ", *onsub2

	pos 20, 30
	/***リストビューの設置***/
	My_CreateLv 500, 280, id_cpumoni, hLVcsouko
	My_InsertColumn id_cpumoni, cpu_culm, c_culm_num, c_clum_w
	My_InsertLvItem id_cpumoni, cpu, cpu_cunt, c_culm_num

	pos 550, 30
	/***リストビューの設置***/
	My_CreateLv 500, 280, id_gpumoni, hLVgsouko
	My_InsertColumn id_gpumoni, gpu_culm, g_culm_num, g_clum_w
	My_InsertLvItem id_gpumoni, gpu, gpu_cunt, g_culm_num

	pos 20, 320
	/***リストビューの設置***/
	My_CreateLv 500, 280, id_rommoni, hLVrsouko
	My_InsertColumn id_rommoni, rom_culm, r_culm_num, r_clum_w
	My_InsertLvItem id_rommoni, rom, rom_cunt, r_culm_num

	oncmd gosub *listnotify, WM_NOTIFY

	/******フォントの反映******/
	sendmsg hLVcsouko, $30, , hFontn
	sendmsg hLVgsouko, $30, , hFontn
	sendmsg hLVrsouko, $30, , hFontn
	/***SUB1_exここまで***/


/*******************************************************************************
								SUB1_ex
********************************************************************************/

	bgscr SUB1_ex, 1100, 625, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hwindow

	font "メイリオ", 16 : objmode 2, 0

	pos 20, 0
	mes "bgscr命令による専用画面遷移動作実験"
	x1 = ginfo(14)
	mes "こちらはSUB1_exです。"

	objsize 100, 25

	pos x1 + 30, 0
	button gosub "MAIN_exへ", *onmain

	pos x1 + 30 + 160, 0
	button gosub "SUB2_exへ", *onsub2

	pos 875, 50
	mesbox mousmoni, 200, 30, 0
	id_mousmoni = stat

	pos 10, 50
	font msgothic, 15
	mesbox logfile_ex, 845, 500, 0
	/***MAIN_exここまで***/



/*******************************************************************************
								SUB2_ex
********************************************************************************/

	bgscr SUB2_ex, 1100, 625, 2
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hwindow

	font "メイリオ", 16 : objmode 2, 0

	pos 20, 0
	mes "bgscr命令による専用画面遷移動作実験"
	x3 = ginfo(14)
	mes "こちらはSUB2_exです。", 1
	objsize 100, 25

	pos x3 + 30, 0
	button gosub "MAIN_exへ", *onmain

	pos x3 + 30 + 160, 0
	button gosub "SUB1_exへ", *onsub1



	/***タブコントロール***/
	pos 50, 100
	My_CreateTab 600, 300, hTab_ex

	InsertTab tabname(0), tabid1, 0, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ1 です。"


	InsertTab tabname(1), tabid2, 1, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ2 です。"


	InsertTab tabname(2), tabid3, 2, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ3 です。"


	InsertTab tabname(3), tabid4, 3, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ4 です。"


	InsertTab tabname(4), tabid5, 4, hTab_ex
	font "メイリオ", 16
	pos 20, 20
	mes "こちらは Tabのつまみ5 です。"


	gsel tabid1, 1

	gsel SUB2_ex
	oncmd gosub *notify_ex, WM_NOTIFY
	/***SUB2_exここまで***/

	gsel MASTER_ex, 1
	gsel SUB1_ex, 1


*maint
	wait 5
	//redraw 0
	gsel SUB1_ex
	mousmoni = ""
	mousmoni += " x = " + rmousex() + "  y = " + rmousey()
	objprm id_mousmoni, mousmoni
	//redraw 1
	goto *maint


//############################## < 以下処理系 > #################################


/*********ウィンドウ切り替え処理*********/

*onmain
	gsel SUB1_ex, -1
	gsel SUB2_ex, -1

	gsel MAIN_ex, 1
	//N OTE メイン画面からbg画面に遷移する際の注意
	//メイン画面を gsel 0, -1 としてはダメ
	return

*onsub1
	gsel MAIN_ex, -1
	gsel SUB2_ex, -1

	gsel SUB1_ex, 1
	/* N OTE bg画面からメイン画面に戻る際の注意
	描画、操作先ウィンドウを一度親玉に戻さないと
	その後の分ジェクトの更新でエラーになる
	描画･操作先の管理は意識して*/
	return

*onsub2
	gsel SUB1_ex, -1
	gsel MAIN_ex, -1

	gsel SUB2_ex, 1
	return
/******ウィンドウ切り替え処理ここまで*****/



//タブコントロールの切り替え
*notify_ex
	My_ChangeTab tabid1, hTab_ex, SUB2_ex
	return


//選択したリストアイテムの取得
*getitem
	gsel MAIN_ex
	sdim infonoteC, 1024 : sdim infonoteG, 1024 : sdim infonoteR, 1024 : sdim infotext, 1024
	My_GetLvItem id_cpumoni, 6, infonoteC
	My_GetLvItem id_gpumoni, 6, infonoteG
	My_GetLvItem id_rommoni, 6, infonoteR
	infotext += "---CPU---" + infonoteC + "\n---GPU---" + infonoteG + "\n---ROM---" + infonoteR
	dialog "購入リスト\n-----------------\n" + infotext + "\n\n\t\t以上を購入しますか?", 0, "購入確認"
	return


//並べ替え
*listnotify
	/***描画先・操作先***/
	gsel MAIN_ex

	sql_open f
	sql_q "BEGIN;"

	dupptr nmhdr, lparam, 12, 4 : id = nmhdr(0)
	if (nmhdr(2) = LVN_COLUMNCLICK) {

		dupptr nmlv, lparam, 40, 4 : inde = nmlv(4)

		switch id

			/***CPUの処理***/
			case hLVcsouko
				sdim cpu, 2048 : sdim data_cpu, 1024
				Tyuc = Tyuc * -1
				if Tyuc == -1 {
					sql_q "SELECT * FROM MyCPU ORDER BY " + cpu_culm(inde) + " DESC;"
				}
				else {
					sql_q "SELECT * FROM MyCPU ORDER BY " + cpu_culm(inde) + " ASC;"
				}
				/***今あるリストアイテムの削除***/
				DelListItem id_cpumoni
				cpu_culm = sql_collist() : split cpu_culm, ",", cpu_culm
				Datainput cpu_cunt, c_culm_num, cpu_culm, cpu
				My_InsertLvItem id_cpumoni, cpu, cpu_cunt, c_culm_num
				swbreak

			/***GPU***/
			case hLVgsouko
				sdim gpu, 2048 : sdim data_gpu, 1024
				Tyug = Tyug * -1
				if Tyug == -1 {
					sql_q "SELECT * FROM MyGPU ORDER BY " + gpu_culm(inde) + " DESC;"
				}
				else {
					sql_q "SELECT * FROM MyGPU ORDER BY " + gpu_culm(inde) + " ASC;"
				}
				DelListItem id_gpumoni
				gpu_culm = sql_collist() : split gpu_culm, ",", gpu_culm
				Datainput gpu_cunt, g_culm_num, gpu_culm, gpu
				My_InsertLvItem id_gpumoni, gpu, gpu_cunt, g_culm_num
				swbreak

			/***ROM***/
			case hLVrsouko
				sdim rom, 2048 : sdim data_rom, 1024
				Tyur = Tyur * -1
				if Tyur == -1 {
					sql_q "SELECT * FROM MyROM ORDER BY " + rom_culm(inde) + " DESC;"
				}
				else {
					sql_q "SELECT * FROM MyROM ORDER BY " + rom_culm(inde) + " ASC;"
				}
				DelListItem id_rommoni
				rom_culm = sql_collist() : split rom_culm, ",", rom_culm
				Datainput rom_cunt, r_culm_num, rom_culm, rom
				My_InsertLvItem id_rommoni, rom, rom_cunt, r_culm_num
				swbreak

		swend

		sql_q "COMMIT;"
		sql_close
	}
	return
#endif


#if 0
/*******************************************************************************
		SQLite DB Table Schema Infomation for HSP3.7beta (SQLele)
		対象となるDBファイル内のテーブルスキーマを確認するスクリプト
********************************************************************************/
	#include "hsp3util.as"
	#include "sqlele.hsp"
	#include "00_module.hsp"
	screen 0, 1250, 450, 2 : title "SQLite DB Table Schema Information for HSP" + strf("%.3f", double(strf("%4x", hspver)) / 1000) + " (SQLele)"
	font "メイリオ", 12 : mref BMSCR, 67 : hFont = BMSCR(38) : objmode 2, 1

	sdim tz1, 2048 : sdim tz2, 2048 : sdim tz3, 512
	tzw = 50, 90, 89, 75, 920 : sdim f

	pos 5, 10 : mes "DataBase File : ", 1

	pos ginfo(14) + 10,
	winobj "static", f, , $50000000 | SS_SUNKEN, 700, 25
	id_file = stat : hStfile = objinfo(id_file, 2)

	objsize 100, 30
	pos ginfo(14) + 720, 5
	button gosub "OPEN", *OPEN
	id_butop = stat : hButop = objinfo(id_butop, 2)

	pos 10, 50
	My_CreateLv 1230, 300, id_dbinfo, hLVdbinfo
	sendmsg hLVdbinfo, 0x1036, , 0x00000020 | 0x00000010

	/***Create Dummy DB***/
	sql_open f

	sql_q "BEGIN;"
	sql_q "SELECT * FROM sqlite_master;"
	repeat length(tmparr)
		tz3 += "" + tmparr(cnt, length2(tmparr) - 1) + ","
	loop
	split tz3, ",", tz3
	My_InsertColumn id_dbinfo, tz3, length(tmparr), tzw, 0
	sql_q "COMMIT;"

	sql_close

	/***フォントの適用***/
	sendmsg objinfo(id_dbinfo, 2), $30, hFont
	gsel 0, 1
	stop

*OPEN
	dialog "db", 16, "DataBase"
	if stat = 0 : return
	f = refstr : fpath = getpath(f, 2)
	if fpath != ".db" {
		dialog "This file is not DataBase file!", 1, "FileError"
		sdim f : return
	}
	sendmsg hStfile, $C, , f
	sdim tz1, 2048 : sdim tz2, 2048 : sdim tz3, 512

	/*** DB OPEN ***/
	sql_open f

	sql_q "BEGIN;"
	sql_q "SELECT * FROM sqlite_master;"

	repeat length2(tmparr) - 1
		tz2 += "" + tmparr(0, cnt) + "|" + tmparr(1, cnt) + "|" + tmparr(2, cnt) + "|" + tmparr(3, cnt) + "|" + tmparr(4, cnt) + "|"
	loop
	split tz2, "|", tz2
	DelListItem id_dbinfo
	/***Item reload***/
	My_InsertLvItem id_dbinfo, tz2, length2(tmparr) - 1, length(tmparr)

	sql_q "COMMIT;"

	sql_close
	/*** DB CLOSE ***/

	return
#endif

#if 0
/*##############################################################################
								マウス座標の取得
###############################################################################*/
	#include "user32.as"
	//#include "00_module.hsp"

#module
#deffunc rm_init
	WakuSize = (ginfo(10) - ginfo(12)) / 2
	TabSize = ginfo(11) - ginfo(13) - WakuSize
	return

#defcfunc rmousex
	x1 = ginfo(0) - ginfo(4) - WakuSize
	return x1

#defcfunc rmousey
	y1 = ginfo(1) - ginfo(5) - TabSize
	return y1
#global
	screen 0, 800, 500, 2
	hwin = hwnd
	rm_init
	sdim moni, 512

	bgscr 1, 500, 300, 2, 100, 50
	SetWindowLong hwnd, -16, WS_CHILD
	SetParent hwnd, hwin

	color 125, 125, 125
	boxf
	font "メイリオ", 16
	pos 50, 50
	mesbox moni, 200, 150, 1
	id_moni = stat

	gsel 1, 1
	gsel 0, 1

*main
	wait 5
	redraw 0
	title " x = " + rmousex() + "  y = " + rmousey()
	redraw 1
	goto *main

#endif