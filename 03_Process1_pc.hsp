/***メインループ***/
*main
	gsel ID_TAB_SETTEI : objprm id_savebut, savebut_text(setting(0, 0))
	//ループ制御ボタンの表示切り替え
	if setting(5, 0) == -1 {
		gsel ID_WIN_MAIN : objprm id_opebut, opebut_text(0)
		wait 32
		goto *main
	}
	else {
		gsel ID_WIN_MAIN : objprm id_opebut, opebut_text(1)
	}

	/***変数処理と描画***/
	redraw 0

	gt(6, 0) = str(int(gt(6, 0)) + 1) //ゲーム内時間カウンタ
	if int(gt(6, 0)) > 5000 : gt(6, 0) = "1"

	//カウンター変速機
	gtcontrol = int(gt(6, 0)) \ setting(4, 0)

	if gtcontrol == 0 {
		gt(5, 0) = strf("%02d", int(gt(5, 0)) + 1)
		st(2, 0) = str(double(st(2, 0)) + 0.07)
		st(3, 0) = str(double(st(3, 0)) + 0.1)
		st(6, 0) = str(double(st(6, 0)) - 0.01)
	}

	/***単位変換チェック***/
	if int(st(2, 0)) > 1023 : objprm layid0, 0
	if int(st(3, 0)) > 1023 : objprm layid0, 1

	await 32 : redraw 1
	/***オートセーブ***/
	gosub *chautosave
	goto *main

//オートセーブ
*chautosave
	//設定が有効かどうか
	if setting(0, 0) == 1 {
		savecontrol = int(gt(6, 0)) \ 10
		if savecontrol == 0 : savec = 1 + savec
		if savec > 5000 {
			//ファイルの確認
			exist fname
			if strsize == -1 {
				fname = savedir + "\\autosave.var"
			}
			mysave u, st, gt, setting, motimono, fname
			astip = "asc"
			objprm layid0, astip
			gsel ID_TAB_LOG, 0 : objprm id_logmoni, logmoni
			gsel ID_WIN_MAIN
			savec = 0
		}
	}
	return

//メニューバー
*OnCommand
	cmd = wparam & 0xFFFF
	switch cmd
		//ファイル(F) >> 開く
		case CMD_OPEN
			setting(5, 0) = -1
			dialog "var", 16, "SaveData"
			if stat = 0 : return
			fname = refstr
			vload fname
			//メインウィンドウ更新
			gsel ID_WIN_MAIN, 0 : redraw 0 : redraw 1
			gsel ID_TAB_MOTI, 0 : objprm id_motimono, motimono
			gsel ID_TAB_SETTEI, 0
			objprm id_savebut, setting(0, 0)
			objprm id_chkosoi, setting(1, 0)
			objprm id_chkfutu, setting(2, 0)
			objprm id_chkhaya, setting(3, 0)
			gsel ID_WIN_MAIN, 0 : objprm layid0, "load"
			gsel ID_TAB_LOG, 0 : objprm id_logmoni, logmoni
			gsel ID_WIN_MAIN
			swbreak

		//ファイル(F) >> 保存
		case CMD_SAVE
			setting(5, 0) = -1
			dialog "var", 17, "SaveData"
			if stat = 0 : return
			fname = refstr
			//拡張子
			exname = getpath(fname, 2)
			if exname = "" : fname += ".var"
			mysave u, st, gt, setting, motimono, fname
			gsel ID_WIN_MAIN, 0 : objprm layid0, "Save"
			gsel ID_TAB_LOG, 0 : objprm id_logmoni, logmoni
			gsel ID_WIN_MAIN
			swbreak

		//ファイル(F) >> 終了
		case CMD_QUIT
			setting(5, 0) = -1
			exist fname
			if strsize == -1 {
				fname = savedir + "\\autosave.var"
			}
			mysave u, st, gt, setting, motimono, fname
			//sql_q "COMMIT;"
			//sql_close
			oncmd 0
			end
			swbreak

		//ヘルプ(H) >> このソフトについて
		case CMD_INFO
			setting(5, 0) = -1
			gsel ID_WIN_INFO, 1
			swbreak

		case CMD_START
			setting(5, 0) = -1
			exist fname
			if strsize == -1 {
				fname = savedir + "\\autosave.var"
			}
			mysave u, st, gt, setting, motimono, fname
			gsel ID_WIN_MAIN, -1
			gsel ID_WIN_OPEN, 1
			swbreak
	swend
	return

//スタート/ストップ
*ope
	setting(5, 0) = setting(5, 0) * -1
	gsel ID_TAB_MOTI, 0 : objprm id_motimono, motimono
	gsel ID_TAB_LOG, 0 : objprm id_logmoni, logmoni
	gsel ID_WIN_MAIN
	return

//タブコントロールの処理
*notify
	dupptr nmhdr, lparam, 12, 4 : tabhwnd = nmhdr(0)
	/***タブコントロール切り替え処理***/
	if (nmhdr(2) == -551) {
		switch tabhwnd
			/***メインのタブコントロール***/
			case hTab_main
				gsel wID + ID_TAB_MOTI, -1
				sendmsg hTab_main, $130B
				wID = stat
				gsel wID + ID_TAB_MOTI, 1
				gsel ID_WIN_MAIN
				swbreak
			/***入れ子のタブコントロール***/
			case hTabOMI
				setting(5, 0) = -1
				ChangeTab ID_OMTAB_CPU, hTabOMI
				gsel ID_TAB_OMISE
				swbreak
		swend
	}
	if (nmhdr(2) == LVN_COLUMNCLICK) {
		dupptr nmlv, lparam, 40, 4 : index = nmlv(4)
		setting(5, 0) = -1
		sql_open db
		sql_q "BEGIN;"
		switch tabhwnd
			/***CPU***/
			case hLVCPU
				sdim cpu, 2048
				swc = swc * -1
				if swc == -1 {
					sql_q "SELECT * FROM MyCPU ORDER BY " + col_clis(index) + " DESC;"
				}
				else {
					sql_q "SELECT * FROM MyCPU ORDER BY " + col_clis(index) + " ASC;"
				}
				col_clis = sql_collist() : split col_clis, ",", col_clis
				Datainput rec_cnum, col_cnum, col_clis, cpu
				gsel ID_OMTAB_CPU
				DelListItem id_LVCPU
				My_InsertLvItem id_LVCPU, cpu, rec_cnum, col_cnum
				swbreak
			case hLVGPU
				swbreak
			case hLVROM
				swbreak
		swend
		gsel ID_WIN_MAIN
		sql_q "COMMIT;"
		sql_close
	}
	return

//################################################OP画面処理

*OP新規
	gsel ID_WIN_OPEN, -1
	gsel ID_WIN_MAIN, 1
	return

*OPload
	setting(5, 0) = -1
	dialog "var", 16, "SaveData"
	if stat = 0 : return
	fname = refstr
	vload fname
	//メインウィンドウ更新
	gsel ID_WIN_MAIN, 0 : redraw 0 : redraw 1
	gsel ID_TAB_MOTI, 0 : objprm id_motimono, motimono
	gsel ID_TAB_SETTEI, 0
	sendmsg hButton, $F1, setting(0, 0)
	objprm id_chkosoi, setting(1, 0)
	objprm id_chkfutu, setting(2, 0)
	objprm id_chkhaya, setting(3, 0)
	gsel ID_WIN_OPEN, -1
	gsel ID_TAB_LOG, 0 : objprm id_logmoni, logmoni
	gsel ID_WIN_MAIN, 1 : objprm layid0, "load"
	return

//======================================設定タブ処理

*tekiou
	sendmsg hChkbox1, $F0 : setting(1, 0) = stat
	sendmsg hChkbox2, $F0 : setting(2, 0) = stat
	sendmsg hChkbox3, $F0 : setting(3, 0) = stat

	if setting(1, 0) == 1 {
		setting(4, 0) = 30
		string2 = " GameSpeed [Slowest]"
	}
	if setting(2, 0) == 1 {
		setting(4, 0) = 6
		string2 = " GameSpeed [Normal]"
	}
	if setting(3, 0) == 1 {
		setting(4, 0) = 3
		string2 = " GameSpeed [Fastest]"
	}
	gsel ID_WIN_MAIN : objprm layid0, "auto_chk"
	gsel ID_TAB_LOG, 0 : objprm id_logmoni, logmoni
	gsel ID_WIN_MAIN
	return

//情報ウィンドウを閉じるボタン
*subw_exi
	gsel ID_WIN_INFO, -1
	return

//終了の処理
*exit
	setting(5, 0) = -1
	winID = wparam
	switch winID
		//------------------------------Master Window
		case ID_WIN_MASTER
			exist fname
			if strsize == -1 {
				fname = savedir + "\\autosave.var"
			}
			mysave u, st, gt, setting, motimono, fname
			oncmd 0
			//sql_q "COMMIT;"
			//sql_close
			end
			swbreak
		//------------------------------情報ウィンドウ
		case ID_WIN_INFO
			gsel ID_WIN_INFO, -1
			swbreak
	swend
	return